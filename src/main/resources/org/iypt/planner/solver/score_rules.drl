package org.iypt.core;
    dialect "java"

import java.util.Collection;

import org.drools.planner.core.score.buildin.hardandsoft.HardAndSoftScoreHolder;
import org.drools.planner.core.score.constraint.IntConstraintOccurrence;
import org.drools.planner.core.score.constraint.ConstraintType;

import org.iypt.planner.domain.Conflict;
import org.iypt.planner.domain.CountryCode;
import org.iypt.planner.domain.DayOff;
import org.iypt.planner.domain.Group;
import org.iypt.planner.domain.Juror;
import org.iypt.planner.domain.Jury;
import org.iypt.planner.domain.JurySeat;
import org.iypt.planner.domain.Round;
import org.iypt.planner.domain.Team;
import org.iypt.planner.domain.Tournament;

global HardAndSoftScoreHolder scoreHolder;

// ############################################################################
// Hard constraints
// ############################################################################

rule "multipleSeatsInRound"
when
    $r : Round()
    $juror : Juror()
    $s1 : JurySeat( juror == $juror, jury.group.round == $r )
    $s2 : JurySeat( juror == $juror, jury.group.round == $r, this != $s1 )
then
    insertLogical( new IntConstraintOccurrence(
        drools.getRule().getName(),
        ConstraintType.NEGATIVE_HARD, 10,
        $juror, $s1, $s2
    ));
end

rule "juryNotFull"
    enabled false // probably not needed
when
    $seat : JurySeat( juror == null, $jury : jury )
then
    System.out.println("juror missing");
    insertLogical( new IntConstraintOccurrence(
        drools.getRule().getName(),
        ConstraintType.NEGATIVE_HARD, 1,
        $seat
    ));
end

rule "invalidChair"
when
    $juror : Juror( !chairCandidate )
    $seat  : JurySeat( chair, juror == $juror )
then
    insertLogical( new IntConstraintOccurrence(
        drools.getRule().getName(),
        ConstraintType.NEGATIVE_HARD, 1,
        $seat
    ));
end

rule "teamAndJurorSameCountry"
when
    $seat : JurySeat( $jury : jury, $juror : juror != null )
    $team : Team( $jury.group.teams contains this )
    $conflict : Conflict( juror == $juror, country == $team.country )
    // or $jury.group.countries.contains($juror.country) ?
then
    insertLogical( new IntConstraintOccurrence(
        drools.getRule().getName(),
        ConstraintType.NEGATIVE_HARD, 1,
        $seat, $team, $conflict
    ));
end

rule "dayOff"
when
    $r : Round( $day : day)
    $m : JurySeat( jury.group.round == $r, $juror : juror != null )
    $dayOff : DayOff( juror == $juror, day == $day )
then
    insertLogical( new IntConstraintOccurrence(
        drools.getRule().getName(),
        ConstraintType.NEGATIVE_HARD, 1,
        $m, $dayOff
    ));
end

rule "teamAndChairMeetOften"
when
    $juror : Juror()
    $team  : Team()
    // collection of all chair seats occupied by $juror and judging $team
    $c     : Collection( size > 2 )
             from collect( JurySeat( chair, juror == $juror, jury.group.teams contains $team ) )
then
    insertLogical( new IntConstraintOccurrence(
        drools.getRule().getName(),
        ConstraintType.NEGATIVE_HARD, 1,
        $team, $c, $juror
    ));
end

// ############################################################################
// Soft constraints
// ############################################################################

// TODO

// ############################################################################
// Calculate score
// ############################################################################

// Accumulate hard constraints
rule "hardConstraintsBroken"
        salience -1 // Do the other rules first (optional, for performance)
    when
        $hardTotal : Number() from accumulate(
            IntConstraintOccurrence(constraintType == ConstraintType.NEGATIVE_HARD, $weight : weight),
            sum($weight) // Vote for http://jira.jboss.com/jira/browse/JBRULES-1075
        )
    then
        scoreHolder.setHardConstraintsBroken($hardTotal.intValue());
end

// Accumulate soft constraints
rule "softConstraintsBroken"
        salience -1 // Do the other rules first (optional, for performance)
    when
        $softTotal : Number() from accumulate(
            IntConstraintOccurrence(constraintType == ConstraintType.NEGATIVE_SOFT, $weight : weight),
            sum($weight) // Vote for http://jira.jboss.com/jira/browse/JBRULES-1075
        )
    then
        scoreHolder.setSoftConstraintsBroken($softTotal.intValue());
end
