package org.iypt.core;
    dialect "java"

import org.drools.planner.core.score.buildin.hardandsoft.HardAndSoftScoreHolder;
import org.drools.planner.core.score.constraint.IntConstraintOccurrence;
import org.drools.planner.core.score.constraint.ConstraintType;

import org.iypt.domain.Conflict;
import org.iypt.domain.Country;
import org.iypt.domain.DayOff;
import org.iypt.domain.Group;
import org.iypt.domain.Juror;
import org.iypt.domain.Jury;
import org.iypt.domain.JuryMembership;
import org.iypt.domain.Round;
import org.iypt.domain.Team;
import org.iypt.domain.Tournament;

global HardAndSoftScoreHolder scoreHolder;

// ############################################################################
// Hard constraints
// ############################################################################

rule "multipleMembershipsInRound"
    enabled true
when
    $r : Round()
    $m1 : JuryMembership( $juror : juror != null, jury.group.round == $r )
    $m2 : JuryMembership( this != $m1, juror ==  $juror, jury.group.round == $r)
then
    //System.out.println("double membership of " + $juror + " in round " + $r);
    insertLogical(new IntConstraintOccurrence("multipleMembershipsInRound",
        ConstraintType.NEGATIVE_HARD, 10, $juror, $m1, $m2 ));
end
    

rule "juryNotFull" // broken?
    enabled false
    //salience 10 // populate juries first
when
    $membership : JuryMembership( juror == null, $jury : jury )
then
    System.out.println("juror missing");
    insertLogical(new IntConstraintOccurrence("juryNotFull",
        ConstraintType.NEGATIVE_HARD, 1, $membership));
        //ConstraintType.NEGATIVE_HARD, 1));
end

rule "teamAndJurorSameCountry"
    enabled true
when
    $m : JuryMembership( $jury : jury, $juror : juror != null )
    $team : Team( country == $juror.country, $jury.group.teams contains this )
    // or $jury.group.countries.contains($juror.country) ?
then
    //System.out.println("same country");
    insertLogical(new IntConstraintOccurrence("teamAndJurorSameCountry",
        ConstraintType.NEGATIVE_HARD, 1, $m, $team));
end

// ############################################################################
// Soft constraints
// ############################################################################

// TODO

// ############################################################################
// Calculate score
// ############################################################################

// Accumulate hard constraints
rule "hardConstraintsBroken"
        salience -1 // Do the other rules first (optional, for performance)
    when
        $hardTotal : Number() from accumulate(
            IntConstraintOccurrence(constraintType == ConstraintType.NEGATIVE_HARD, $weight : weight),
            sum($weight) // Vote for http://jira.jboss.com/jira/browse/JBRULES-1075
        )
    then
        scoreHolder.setHardConstraintsBroken($hardTotal.intValue());
end

// Accumulate soft constraints
rule "softConstraintsBroken"
        salience -1 // Do the other rules first (optional, for performance)
    when
        $softTotal : Number() from accumulate(
            IntConstraintOccurrence(constraintType == ConstraintType.NEGATIVE_SOFT, $weight : weight),
            sum($weight) // Vote for http://jira.jboss.com/jira/browse/JBRULES-1075
        )
    then
        scoreHolder.setSoftConstraintsBroken($softTotal.intValue());
end
